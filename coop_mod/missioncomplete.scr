//[200] chrissstrahl - 2020.06.14
//this is a coop specific mission complete file

//=========================================================================
main local.nextlevel local.bsp local.skipFade:{
//=========================================================================
if(level.cMTE_coop_missioncomplete){if(!level.cMTE){level.cMTE=0}; level.cMTE++; println( "-#-#- thread missioncomplete/main->"+level.cMTE+"" )} //[202] chrissstrahl - this is to debug the coop mod thread handling
	//[200] Smithy - store the thread name
	local.threadName = "coop_mod/missioncomplete.scr::main"
	
	//[200] chrissstrahl - set next map to be active map for dedicated server reboot
	if(level.coop_dedicated != 0){
		setcvar "ui_startmap" local.nextlevel
	}
	
	//chrissstrahl - stop watch for all players
	exec coop_mod/replace.scr::stopwatch 0

	//[200] Smithy - used to reload sv_maplist on next load
	//[200] Smithy - so our coop mission transition works
	setcvar "coop_prevMapList" (getcvar sv_maplist) 
	setcvar "sv_maplist" ""				
	
	//[200] Smithy - stop any further changes via main.scr::changeGameType if there are any
	level.coop_preventGameTypeChanges = game.true
	
	//[200] Smithy - stop the thread if one is running
	if (level.coop_changeGameTypeThread){  										
		thread coop_mod/main.scr::changeGameTypeDebug ("(" + local.threadName + "): Terminating " + level.coop_changeGameTypeThread.threadName + " thread for level change, and stopping any future calls.")
		level.coop_changeGameTypeThread end
	}
	
	local.gametype = getcvar( "g_gametype" )
	setcvar "g_gametype" "0"
	
	game.loadout = false	
	bsptransition local.nextlevel local.skipFade
	
	setcvar "g_gametype" local.gametype
}end