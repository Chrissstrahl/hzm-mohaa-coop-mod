//============================================================
main: {
//============================================================
	level.script = "maps/e1l4.scr" 							//chrissstrahl
	//[201] Smithy - removed redundant noise from script
	
	
	//[201] Smithy - initial inventory is handled in coop_mod/loadout.scr
	//see e1l4/Intro.scr::DoIntro for silenced pistol weapon assignment (when player gets out of a truck)

	//@chrissstrahl //@smithy - this needs to be DISABLED - this is just here to speed up testing
	level.coop_debugSpawn = 1								//[202] chrissstrahl - for debug
	level.coop_devTimescaleOn = 0
	level.coop_disableSpawnWarper	= game.true					//chrissstrahl - disable respawn warper
	waitthread coop_mod/main.scr::main 					//chrissstrahl - start coop mod
	//thread coop_mod/butler.scr::e1l4_start				//chrissstrahl - remove stuff that we don't need in mp
	
	//[201] Smithy - make players disguised on spawn
	level.coop_enableDisguises = game.true
	level.coop_uniformOnSpawn = game.true
	level.coop_disguiseUniform = "sc_ax_ital_inf" //[201] Smithy - Italian army

	exec global/auto.scr e1l4
	waitexec maps/e1l4/hacks.scr

	//Crimewavez - coop compatibility.
	//level waittill prespawn
	waitthread coop_mod/replace.scr::waitTillPrespawn
	
	//[202] Smithy - find the weapons by origin and also feed in amount of starting ammo
	if (level.gametype){
		thread coop_weaponLoadoutTrigger ( -3221.00 5011.00 156.00) 64 	//[202] Smithy - find the mp40 in the map room and make it add to loadout on pickup
		thread coop_weaponLoadoutTrigger ( -3726.00 3537.00 -233.00) 64 //[202] Smithy - find the mp40 on the lower deck (just in case one in map room wasn't found)
		thread coop_weaponLoadoutTrigger ( -3318.00 4194.00 -56.00) 28 	//[202] Smithy - find the shotgun and and make it add to loadout on pickup
	}	

	exec global/ambience.scr e1l4
	
	// this is what scales the enemy health, 1.0 = 100%
	level.enemyhealthscalar = 0.8

	spawn trigger_once targetname "musicTransitionCoverBlown"
	spawn trigger_once targetname "musicTransitionKnockedOut"

	level.playerBoardedShip = 0
	waitthread maps/e1l4/Intro.scr::InitIntro
	waitthread maps/e1l4/PreShip.scr::InitPreShip
	waitthread maps/e1l4/Ship.scr::InitShip
	waitthread maps/e1l4/Maproom.scr::InitMaproom	
	waitthread maps/e1l4/SunkShip.scr::InitSunkShip
	waitthread maps/e1l4/PostShip.scr::InitPostShip
	waitthread maps/e1l4/Escape.scr::InitEscape  //car chase
	thread animateFans //chrissstrahl changed to thread, so we can have more delay to reduce events in setup
	waitthread InitLevel

	$mg42 thread initMg42

	//Crimewavez - coop compatibility.
	//level waittill spawn
	waitthread coop_mod/replace.scr::waitForPlayer
	
	// have hack, have gun...
	if (level.skipintro != 0 || level.skipobtainpapers != 0 || level.skipboardship != 0 || 
		level.skipobjmaproom != 0 || level.skipfindexplosives != 0 || level.skipshipexplode != 0 || 
		level.skipescapeship != 0 || level.skipreturntoklaus != 0 ||level.skipescapetown != 0){
		
		// Hand the player the gun
		if(level.gametype == 0){
			$player item weapons/silencedpistol.tik  
			$player ammo pistol 16
			$player item weapons/Uk_W_Vickers.tik
			$player ammo mg 500
		}else{
			//[201] Smithy - coop weapon assignment for hacks
			thread coop_mod/replace.scr::givePlayerWeapon "models/weapons/silencedpistol.tik" 	
			thread coop_mod/replace.scr::ammo pistol 16
			thread coop_mod/replace.scr::givePlayerWeapon "models/weapons/uk_w_vickers.tik" //[201] Smithy - make sure weapon assignments are all lowercase
			thread coop_mod/replace.scr::ammo mg 500
		}
	}

	// Intro
	waitthread maps/e1l4/Intro.scr::DoIntro

	//Crimewavez - Only in SP.
	if(level.gametype == 0) {
		exec global/autosave.scr level.autosave	//"Arrival"
		level.autosave++
	}

	// Find a way aboard the ship...
	waitthread global/objectives.scr::add_objectives level.objObtainPapers 2
	waitthread global/objectives.scr::current_objectives level.objObtainPapers

	//waitthread maps/e1l4/PreShip.scr::ObtainPapers
	waitthread maps/e1l4/PreShip.scr::ObtainPapers

	// move the compass to point to the ship
	waitthread global/objectives.scr::add_objectives level.objObtainPapers 2 level.objObtainPapersText $shipentrance.origin
	waitthread global/objectives.scr::current_objectives level.objObtainPapers

	// Board Ship
	//waitthread global/objectives.scr::add_objectives level.objBoardShip 2
	//waitthread global/objectives.scr::current_objectives level.objBoardShip
	waitthread maps/e1l4/PreShip.scr::ObjectiveBoardShip
	//waitthread global/objectives.scr::add_objectives level.objBoardShip 3

	level.playerBoardedShip = 1
	waitthread global/objectives.scr::add_objectives level.objObtainPapers 3

	//Crimewavez - Only in SP.
	if(level.gametpye == 0) {
		exec global/autosave.scr level.autosave	//"All Aboard"
		level.autosave++
	}

	// Steal Documents
	waitthread global/objectives.scr::add_objectives level.objStealDocuments 2
	waitthread global/objectives.scr::current_objectives level.objStealDocuments

	if (!level.skipobjmaproom){
		thread maps/e1l4/Ship.scr::compassTracker level.objStealDocuments level.objStealDocumentsText $documents[1].origin
	}

	waitthread maps/e1l4/MapRoom.scr::ObjectiveStealDocuments
	waitthread global/objectives.scr::add_objectives level.objStealDocuments 3


	// Find Explosives
	waitthread global/objectives.scr::add_objectives level.objFindExplosives 2
	waitthread global/objectives.scr::current_objectives level.objFindExplosives

	if (!level.skipfindexplosives){
		thread maps/e1l4/Ship.scr::compassTracker level.objFindExplosives level.objFindExplosivesText $bombs[1].origin 
	}

	waitthread maps/e1l4/Ship.scr::ObjectiveFindExplosives
	waitthread global/objectives.scr::add_objectives level.objFindExplosives 3


	// Place Explosives
	waitthread global/objectives.scr::add_objectives level.objPlaceExplosives 2
	waitthread global/objectives.scr::current_objectives level.objPlaceExplosives

	if (!level.skipshipexplode){
		thread maps/e1l4/Ship.scr::compassTracker level.objPlaceExplosives level.objPlaceExplosivesText $engineroom.origin 
	}

	waitthread maps/e1l4/Ship.scr::ObjectivePlaceExplosives
	waitthread global/objectives.scr::add_objectives level.objPlaceExplosives 3

	// Return to extraction
	waitthread global/objectives.scr::add_objectives level.objReturnToSpy 2
	waitthread global/objectives.scr::current_objectives level.objReturnToSpy
	thread maps/e1l4/Ship.scr::compassTracker level.objReturnToSpy level.objReturnToSpyText $deck1.origin

	// Explode the ship
	waitthread maps/e1l4/SunkShip.scr::ExplodeShip
	
	//Crimewavez - only in SP.	
	if(level.gametype == 0)  {
		exec global/autosave.scr level.autosave	//"Scuttled"
		level.autosave++
	}

	thread autoSaveAtBoatExit
	level.abortCompassTracker = 1
	waitframe

	waitthread global/objectives.scr::add_objectives level.objReturnToSpy 2 level.objReturnToSpyText $t491.origin
	waitthread global/objectives.scr::current_objectives level.objReturnToSpy

	// Go find Klaus
	waitthread maps/e1l4/PostShip.scr::ObjectiveReturnToSpy
	waitthread global/objectives.scr::add_objectives level.objReturnToSpy 3

	//Crimewavez - Only in SP, as you can guess.
	if(level.gametype == 0) {
		exec global/autosave.scr level.autosave	//"The Edge of Town"
		level.autosave++
	}

	// Escape!
	waitthread global/objectives.scr::add_objectives level.objEscape 2
	waitthread global/objectives.scr::current_objectives level.objEscape

// ----FIXME
//	exec global/autosave.scr level.autosave	//"The Getaway"
//	level.autosave++

	// (instead of TruckRide) Escape town with Klaus in tow (after meeting him at crashed truck)
	waitthread maps/e1l4/Escape.scr::Escape

//	// Truck ride up till Klaus parks it
//	waitthread maps/e1l4/Escape.scr::TruckRide

// ----FIXME
//	exec global/autosave.scr level.autosave	//"The Edge of Town"
//	level.autosave++

//	// Blow up the arch!
//	waitthread global/objectives.scr::add_objectives level.objDestroyArch 2
//	waitthread global/objectives.scr::current_objectives level.objDestroyArch
//	waitthread maps/e1l4/Escape.scr::ObjectiveDestroyArch
//	waitthread global/objectives.scr::add_objectives level.objDestroyArch 3
//
//	// Get back in the truck!
//	waitthread global/objectives.scr::add_objectives level.objReturnToTruck 2 "Get back in the truck" $endtruck.origin
//	waitthread global/objectives.scr::current_objectives level.objReturnToTruck
//	waitthread maps/e1l4/Escape.scr::ObjectiveReturnToTruck
//	waitthread global/objectives.scr::add_objectives level.objReturnToTruck 3
//
//	// Make the jump!
//	waitthread maps/e1l4/Escape.scr::Outro
	waitthread global/objectives.scr::add_objectives level.objEscape 3
	wait 0.5

	//end level
	//[200] chrissstrahl - this is not suppose to happen in mp
	//generally changing cvars is a puerly clientside thing
	if(level.gametype == 0){
		setcvar "g_medal3" "1" //Africa Star
		exec global/missioncomplete.scr briefing/briefinge2
	}
	else{
		exec global/missioncomplete.scr (level.coopNextMap) 1
	}
}end

//[202] Smithy - find the weapon and set the method to call
//when picked up so we can add it to loadout
//============================================================
coop_weaponLoadoutTrigger local.origin local.ammo:{
//============================================================	
	local.maxEnts = int(getcvar "maxentities") - 1	

	for (local.i = 0; local.i <= local.maxEnts; local.i++){
		local.ent = getentbyentnum local.i
		if (!local.ent){ continue }
		if (local.ent.origin == local.origin && local.ent.classname == "Weapon"){	
			//println "****SMITHY****: ADDING LOADOUT THREAD FOR: " local.ent.model
			local.ent setthread coop_addWeaponToLoadout
			local.ent.coop_startingAmmo = local.ammo
		}
	}
}end

//[202] Smithy - give current and future players the weapon and ammo
//============================================================
coop_addWeaponToLoadout:{
//============================================================
	thread coop_mod/replace.scr::givePlayerWeapon self.model 	
	thread coop_mod/replace.scr::ammo (waitthread coop_mod/itemhandler.scr::getAmmoType self.model) self.coop_startingAmmo
}end

//============================================================
Fadein:{
//============================================================
	thread doMusic
	waitframe
	thread RealFadein

	//============================================================
	//  8/17/03 JP - I took this out to replace a bug about a really long fadein as well as the fact that there is no date for the beginning
	//  of this level. I replaced it with the RealFadein thread which just does enough of a fade in to hide stuff moving.
	//============================================================
	//	exec global/fadein.scr
	//	waitframe
	//	waitthread global/fadein.scr::DoFadeIn e1l4_1 e1l4_2 e1l4_3 NIL 1
	//============================================================

	//undo any mousemovment during the hud stuff
	thread coop_mod/replace.scr::viewangles (0 3 0) //chrissstrahl
}end

//============================================================
RealFadein:{
//============================================================
	fadeout .001 0 0 0 1
	drawhud 0
	wait 1.5
	
	//[202] chrissstrahl - make sure it looks right
	waitthread coop_mod/replace.scr::waitForPlayer
	fadein 2 0 0 0 1
}end

//============================================================
InitLevel:{
//============================================================
	level.script = "maps/e1l4.scr"
	//level.music  = ""
	waitthread InitFriends
	waitthread InitEnemies
	waitthread InitObjectives
	waitthread InitPlayer
	waitexec maps/e1l4/light_e1l4.scr
	
	setcvar g_mission 1 //[202] Smithy - unsure, best to just leave it //[200] chrissstrahl - not sure if that is for menu only, we will have to keep a eye on that
	level.autosave = 1
}end

//============================================================
InitPlayer:{
//============================================================
	//chrissstrahl - sp only
	if(level.gametype == 0){
		$player heal 1
	}
	
	waitthread Fadein
	
	waitthread GiveObjectives
	waitexec global/loadout.scr
}end

//============================================================
ArmPlayer:{
//============================================================
	iprintlnbold_noloc("ArmPlayer start") //chrissstrahl - i belive this func is not in use
	//[202] chrissstrahl
	if(level.gametype == 0){
		$player takeall
		$player item weapons/M18_smoke_grenade.tik
		$player item weapons/silencedpistol.tik  
		//waitthread global/items.scr::blank_items 
		$player ammo pistol 80
		$player ammo smokegrenade 3
	}
	
	iprintlnbold_noloc("ArmPlayer end") //chrissstrahl - i belive this func is not in use
}end

//============================================================
InitFriends:{
//============================================================
	//none of these
}end

//============================================================
InitEnemies:{
//============================================================
	// Criminal - It was originally empty.
}end

//============================================================
InitObjectives:{
//============================================================
	level.objObtainPapers = 1
	//level.objBoardShip = 2
	level.objStealDocuments = 2
	level.objFindExplosives = 3
	level.objPlaceExplosives = 4
	level.objReturnToSpy = 5
	level.objEscape = 6
	//level.objDestroyArch = 7
	//level.objReturnToTruck = 8
	//level.objEscape = 9
}end

//============================================================
GiveObjectives:{
//============================================================
	level.objObtainPapersText 		= 	"Find a Way Aboard the Freighter"
	level.objStealDocumentsText 	= 	"Obtain the Troop Roster"
	level.objFindExplosivesText 	= 	"Search the Freighter for Explosive Charges"
	level.objPlaceExplosivesText 	= 	"Locate Engine Room and Set Explosives"
	level.objReturnToSpyText 		= 	"Rendezvous with Klaus"

	waitthread global/objectives.scr::add_objectives level.objObtainPapers 		1 level.objObtainPapersText 	$goodpapers.origin
	//waitthread global/objectives.scr::add_objectives level.objBoardShip 		1 "Board the German Ship" 		$shipentrance.origin
	waitthread global/objectives.scr::add_objectives level.objStealDocuments 	1 level.objStealDocumentsText 	$documents[1].origin
	waitthread global/objectives.scr::add_objectives level.objFindExplosives 	1 level.objFindExplosivesText  	$getbombs.origin
	waitthread global/objectives.scr::add_objectives level.objPlaceExplosives 	1 level.objPlaceExplosivesText 	$engineroom.origin
	waitthread global/objectives.scr::add_objectives level.objReturnToSpy 		1 level.objReturnToSpyText 		$EscapeKlausWaitLocation.origin
	//waitthread global/objectives.scr::add_objectives level.objDestroyArch 	1 "Stop your pursuers" 			$archexplosive.origin
	//waitthread global/objectives.scr::add_objectives level.objReturnToTruck 1
	waitthread global/objectives.scr::add_objectives level.objEscape 			1 "Escape the City" 			$cityexit.origin
}end

//============================================================
UpdateObtainPapers:{
//============================================================
	if (level.currentobjective == level.objObtainPapers){
		waitthread global/objectives.scr::add_objectives level.objObtainPapers 2 level.objReturnToSpyText $(self.target).origin
		waitthread global/objectives.scr::current_objectives level.objObtainPapers
	}
}end

//============================================================
doMusic:{
//============================================================
	waitthread coop_mod/replace.scr::tmstartloop "sound/music/Kleveburg.mp3" //chrissstrahl - coop
	//$player stufftext "tmstop; tmstartloop sound/music/Kleveburg.mp3" //chrissstrahl
	$musicTransitionCoverBlown waittill trigger

	waitthread coop_mod/replace.scr::tmstartloop "sound/music/E1L4_Rjuken.mp3" //[202] Smithy - use right music //chrissstrahl - coop
	//$player stufftext "tmstop; tmstartloop sound/music/E1L4_Rjuken.mp3" //chrissstrahl
	$musicTransitionKnockedOut waittill trigger

	waitthread coop_mod/replace.scr::tmstartloop "sound/music/mus_01a_suspense.mp3" //[202] Smithy - use right music //chrissstrahl - coop
	//$player stufftext "tmstop; tmstartloop sound/music/mus_01a_suspense.mp3" //chrissstrahl
	$CompassTargetKlausTrigger waittill trigger

	waitthread coop_mod/replace.scr::tmstartloop "sound/music/E1L4_Rjuken.mp3" //[202] Smithy - use right music //chrissstrahl - coop
	//$player stufftext "tmstop; tmstartloop sound/music/E1L4_Rjuken.mp3" //chrissstrahl
}end

//chrissstrahl - put in a for loop, for better overview and delay handling
//============================================================
animateFans:{
//============================================================
	wait 1 //chrissstrahl - have more delay to reduce events in setup
		
	//chrissstrahl - start them rotating all at once, just to be sure
	local.fanNum = 1
	for(local.fanNum=1;local.fanNum<9;local.fanNum++){
		$("fanblade"+local.fanNum+"a") bind $("fanbase"+local.fanNum)
		$("fanblade"+local.fanNum+"b") bind $("fanbase"+local.fanNum)
		$("fanblade"+local.fanNum+"c") bind $("fanbase"+local.fanNum)
		$("fanblade"+local.fanNum+"d") bind $("fanbase"+local.fanNum)
		waitframe  //chrissstrahl - have more delay to reduce events in setup
	}
	//chrissstrahl - start them rotating all at once, just to be sure
	for(local.fanNum=1;local.fanNum<9;local.fanNum++){
		$("fanbase"+local.fanNum) rotatey 250
	}	
}end

//============================================================
setKlausWaypoint:{
//============================================================
	if (level.currentobjective == level.objReturnToSpy){
		waitthread global/objectives.scr::add_objectives level.objReturnToSpy 2 level.objReturnToSpyText self.target.origin
		waitthread global/objectives.scr::current_objectives level.objReturnToSpy
	}
}end

//============================================================
alarmSwitch:{
//============================================================
	thread maps/e1l4/ship.scr::AlarmSounded
}end

//============================================================
initMg42:{
//============================================================
	// level.spot_burstmin level.spot_burstmax level.spot_burstdownmin level.spot_burstdownmax	
	self burstfiresettings 1 3 1.5 3
	self maxyawoffset 60.0
	self pitchcaps "-20 20 0"
	self convergetime 0.25

	switch (level.skill){
		case 0:
			self aibulletspread 450 275
		break;
		case 1:
			self aibulletspread 400 225
		break;
		case 2:
			self aibulletspread 300 125
		break;
	}
}end

//============================================================
autoSaveAtBoatExit:{
//============================================================
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "deleteguy4spawner"
	local.trigger waittill trigger
	
	//Crimewavez - and again, only in SP, who could predict it.
	if(level.gametype == 0) { 
		exec global/autosave.scr level.autosave	//"The Getaway"
		level.autosave++
	}
}end

